module src/test/anomaly_detection_test

import test
import src/lib/anomaly_detection
import src/lib/event
import src/test/lib

def eventsFromList(values: List[Double]): List[Event] = {
	def loop(vals: List[Double], ts: Int): List[Event] = {
		vals match {
			case Nil() => Nil()
			case Cons(h, t) => Cons(Event(ts, h), loop(t, ts + 1))
		}
	}
	loop(values, 0)
}

def approx(a: Double, b: Double): Bool = {
	abs(a - b) <= 0.000001
}

def sameEvent(a: Event, b: Event): Bool = {
	a.timestamp == b.timestamp && approx(a.value, b.value)
}

def sameEvaluation(a: AnomalyEvaluation, b: AnomalyEvaluation): Bool = {
	a.event.timestamp == b.event.timestamp &&
	approx(a.event.value, b.event.value) &&
	a.isAnomaly == b.isAnomaly &&
	approx(a.anomalyScore, b.anomalyScore)
}

def assertAnomalyDetection(inputs: List[Event], expected: List[AnomalyEvaluation]) { program: () => Unit / { read[Event], AnomalyDetection } } : Unit / { Assertion } = {
	var remainingInputs = inputs
	var remainingExpected = expected

	try {
		program()
	} with read[Event] { () =>
		remainingInputs match {
			case Nil() =>
				resume { do stop() }
			case Cons(head, tail) =>
				remainingInputs = tail
				resume { unbox head }
		}
	} with AnomalyDetection {
		def anomaly(ev: AnomalyEvaluation) = {
			remainingExpected match {
				case Nil() =>
					assertTrue(false, "Received more detection events than expected")
				case Cons(head, tail) =>
					assertTrue(sameEvaluation(head, ev), "Detection event did not match expected anomaly")
					remainingExpected = tail
			}
			resume(())
		}
		def noAnomaly(ev: AnomalyEvaluation) = {
			remainingExpected match {
				case Nil() =>
					assertTrue(false, "Received more detection events than expected")
				case Cons(head, tail) =>
					assertTrue(sameEvaluation(head, ev), "Detection event did not match expected non-anomaly")
					remainingExpected = tail
			}
			resume(())
		}
	}

	assertTrue(remainingInputs.isEmpty(), "Did not read all inputs")
	assertTrue(remainingExpected.isEmpty(), "Did not receive all expected detection events")
}

def testSuite() = suite("anomaly_detection") {
	test("min-max detector flags out-of-range") {
		val input = eventsFromList([1.0, 4.0, -2.0, 5.0, 3.0])
		val expected = [
			AnomalyEvaluation(Event(0, 1.0), false, 0.0),
			AnomalyEvaluation(Event(1, 4.0), false, 0.0),
			AnomalyEvaluation(Event(2, -2.0), true, 2.0),
			AnomalyEvaluation(Event(3, 5.0), true, 1.0),
			AnomalyEvaluation(Event(4, 3.0), false, 0.0)
		]

		assertAnomalyDetection(input, expected) {
			anomaly_detection::minMaxAnomalyDetector(0.0, 4.0)
		}
	}

	test("mean threshold detector flags deviations") {
		val input = eventsFromList([1.0, 2.0, 5.0, 2.0])
		val expected = [
			AnomalyEvaluation(Event(0, 1.0), false, 0.0),
			AnomalyEvaluation(Event(1, 2.0), false, 0.5),
			AnomalyEvaluation(Event(2, 5.0), true, 2.3333333333333335),
			AnomalyEvaluation(Event(3, 2.0), false, -0.5)
		]

		assertAnomalyDetection(input, expected) {
			anomaly_detection::meanThresholdAnomalyDetector(1.5)
		}
	}

	test("z-score detector flags outliers") {
		val input = eventsFromList([1.0, 1.0, 5.0, 1.0])
		val expected = [
			AnomalyEvaluation(Event(0, 1.0), false, 0.0),
			AnomalyEvaluation(Event(1, 1.0), false, 0.0),
			AnomalyEvaluation(Event(2, 5.0), true, 1.1547005383792515),
			AnomalyEvaluation(Event(3, 1.0), false, -0.5)
		]

		assertAnomalyDetection(input, expected) {
			anomaly_detection::zScoreAnomalyDetector(1.0)
		}
	}
}